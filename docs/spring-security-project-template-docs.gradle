plugins {
	alias(libs.plugins.io.spring.antora.generate.antora.yml)
	alias(libs.plugins.org.antora)
	alias(libs.plugins.org.hidetake.ssh)
	id "java"
}

group = project.rootProject.group
version = project.rootProject.version

java {
	sourceCompatibility = JavaVersion.VERSION_17
}

compileJava {
	options.compilerArgs << '-parameters'
}

antora {
	options = [clean: true, fetch: !project.gradle.startParameter.offline, stacktrace: true]
	environment = [
		'BUILD_REFNAME': 'HEAD',
		'BUILD_VERSION': project.version,
	]
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/milestone" }
	maven { url "https://repo.spring.io/snapshot" }
}

dependencies {
	implementation(platform(libs.org.springframework.boot.spring.boot.dependencies))
	implementation "org.springframework.boot:spring-boot-starter-web"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.security:spring-security-test"
}

def generateAttributes() {
	return [
			"spring-security-project-template-version": project.version
	] + resolvedVersions(project.configurations.testRuntimeClasspath)
}

static def resolvedVersions(Configuration configuration) {
	return configuration.resolvedConfiguration
			.resolvedArtifacts
			.collectEntries { [(it.name + '-version'): it.moduleVersion.id.version] }
}

tasks.named("generateAntoraYml") {
	asciidocAttributes = project.provider {
		generateAttributes()
	}
}

tasks.register("api", Javadoc) { api ->
	group = "Documentation"
	description = "Generates aggregated Javadoc API documentation."
	doLast {
		if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
			project.copy {
				from api.destinationDir
				into api.destinationDir
				include "element-list"
				rename "element-list", "package-list"
			}
		}
	}
	source project.rootProject.subprojects
		.findAll { it.plugins.hasPlugin(JavaLibraryPlugin) }
		.collect { it.extensions.getByType(JavaPluginExtension) }
		.collect { it.sourceSets.main.allJava }
}

tasks.register("docsZip", Zip) {
	dependsOn api
	group = "Distribution"
	description = "Build -docs archive containing all docs for deployment at docs.spring.io."
	archiveBaseName.set(project.rootProject.name)
	archiveClassifier.set("docs")
	from (api) {
		into "api"
	}
	into "docs"
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register("docs") {
	dependsOn docsZip
	group = "Documentation"
	description = "An aggregator task to aggregate all the documentation."
}

tasks.register("deployDocs") {
	dependsOn "docsZip"
	group = "Documentation"
	description = "Deploy api documentation to the remote server."

	ssh.settings {
		knownHosts = allowAnyHosts
	}

	remotes {
		docs {
			role "docs"

			// Apply deployDocsHost property
			if (hasProperty("deployDocsHost")) {
				host = findProperty("deployDocsHost").toString()
			} else {
				host = "docs.spring.io"
			}

			// Apply deployDocsSshUsername property
			if (hasProperty("deployDocsSshUsername")) {
				user = findProperty("deployDocsSshUsername").toString()
			}

			// Apply deployDocsSshKeyPath or deployDocsSshKey property
			if (hasProperty("deployDocsSshKeyPath")) {
				identity = file(findProperty("deployDocsSshKeyPath"))
			} else if (hasProperty("deployDocsSshKey")) {
				identity = findProperty("deployDocsSshKey")
			}

			// Apply deployDocsSshPassphrase property
			if (hasProperty("deployDocsSshPassphrase")) {
				passphrase = findProperty("deployDocsSshPassphrase").toString()
			}

			retryCount = 5 // retry 5 times (default is 0)
			retryWaitSec = 10 // wait 10 seconds between retries (default is 0)
		}
	}

	doLast {
		ssh.run {
			session(remotes.docs) {
				def now = System.currentTimeMillis()
				def name = rootProject.name
				def version = rootProject.version
				def tempPath = "/tmp/${name}-${now}-docs/".replaceAll(" ", "_")
				execute "mkdir -p $tempPath"

				tasks.docsZip.outputs.each { o ->
					put from: o.files, into: tempPath
				}

				execute "unzip $tempPath*.zip -d $tempPath"

				def extractPath = "/var/www/domains/spring.io/docs/htdocs/autorepo/docs/${name}/${version}/"

				execute "rm -rf $extractPath"
				execute "mkdir -p $extractPath"
				execute "mv $tempPath/docs/* $extractPath"
				execute "chmod -R g+w $extractPath"
			}
		}
	}
}

tasks.named("test") {
	useJUnitPlatform()
}
