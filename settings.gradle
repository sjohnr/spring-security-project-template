pluginManagement {
	repositories {
		maven {
			url = uri("https://repo.spring.io/snapshot")
		}
		gradlePluginPortal()
	}
}

plugins {
	id "com.gradle.develocity" version "3.18.1"
	id "io.spring.develocity.conventions" version "0.0.22"
}

dependencyResolutionManagement {
	repositories {
		mavenCentral()
	}
}

rootProject.name = 'spring-security-project-template'

def buildFiles = fileTree(rootDir) {
	def excludes = gradle.startParameter.projectProperties.get("excludeProjects")?.split(",")
	include "**/*.gradle", "**/*.gradle.kts"
	exclude "build", "**/gradle", "settings.gradle", "buildSrc", "/build.gradle", ".*", "out"
	if (excludes) {
		exclude excludes
	}
}

buildFiles.forEach { buildFile ->
	if (buildFile.name == "build.gradle" || buildFile.name == "build.gradle.kts") {
		def projectPath = buildFile.parentFile
			.relativePath(projectDir)
			.replace(File.separator, ":")
		include projectPath
	} else {
		def projectName = buildFile.name.endsWith(".gradle.kts")
			? buildFile.name.replace(".gradle.kts", "")
			: buildFile.name.replace(".gradle", "")
		def projectPath = ":$projectName"
		include projectPath

		def project = findProject(projectPath)
		project.name = projectName
		project.projectDir = buildFile.parentFile
		project.buildFileName = buildFile.name
	}
}
